


function on_load minecraft:load{
    say Loading DEMO Magic Spells V2
    # Create the necessary scoreboards
    scoreboard objectives add Timer dummy "Timer"    
    scoreboard objectives add Mana dummy "Mana"    
    scoreboard objectives add Jump minecraft.custom:minecraft.jump "Jump"
    scoreboard objectives add Deaths minecraft.custom:minecraft.deaths "Deaths"
    scoreboard objectives add Sneak_Time minecraft.custom:minecraft.sneak_time "Sneak Time"

    # This is a place holder for Settings and other data
    data modify storage magic:particles text set value "dust{color:[1.0,0.87,0.0],scale:1}"
    data modify storage magic:carpet text set value "minecraft:red_carpet"
    data modify storage magic:music text set value "flute"
}






# This is for book animation to do all the particles and movements, each tag is seperate to play around with settings!
dir book_create_animation {
    dir float {
        #This is to make it floaty
        function movement {
        REPEAT (0, 133, 1) as time {
        execute as @e[tag=book_tp,scores={Timer=<%Math.round(time)%>}] at @s run tp @s ~ ~<%Math.sin(time/10)/7%> ~
        }
        execute as @e[tag=book_tp,scores={Timer=120..}] at @s run particle dust_color_transition{from_color:[0.29,0.01,0.31],to_color:[0.0,0.0,0.0],scale:2} ~ ~ ~ 0.4 0.4 0.4 0 100 normal
        execute as @e[tag=book_tp,scores={Timer=133..}] run kill @s
        }
        function movement_reload {
        scoreboard players set @e[tag=book_tp] Timer 0
        }
        clock time 1t {
        function main_magic:book_create_animation/float/music with storage magic:music
        function main_magic:book_create_animation/float/movement
        scoreboard players add @e[tag=book_tp] Timer 1
        }
        function music {
           execute as @e[tag=book_tp,scores={Timer=2}] at @s run playsound minecraft:block.fire.extinguish record @a[distance=..10] ~ ~ ~ 10 1 1
           $execute as @e[tag=book_tp,scores={Timer=24}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 0.5 1
           $execute as @e[tag=book_tp,scores={Timer=30}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 0.6 1
           $execute as @e[tag=book_tp,scores={Timer=36}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 0.5 1
           $execute as @e[tag=book_tp,scores={Timer=42}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 0.4 1
           $execute as @e[tag=book_tp,scores={Timer=64}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 1.2 1
           $execute as @e[tag=book_tp,scores={Timer=70}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 1.3 1
           $execute as @e[tag=book_tp,scores={Timer=76}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 1.4 1
           $execute as @e[tag=book_tp,scores={Timer=82}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 1.3 1
           $execute as @e[tag=book_tp,scores={Timer=98}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 1.8 1
           $execute as @e[tag=book_tp,scores={Timer=104}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 1.9 1
           $execute as @e[tag=book_tp,scores={Timer=107}] at @s run playsound minecraft:block.note_block.$(text) record @a[distance=..10] ~ ~ ~ 10 2 1
        }
    }


    #This is to make it have particles
    dir particles {
        function fx {
            REPEAT (0, 90, 0.5) as time { 
        $execute as @e[tag=book_particles,scores={Timer=<%Math.round(time)%>}] at @s run particle $(text) ~<%(Math.sin(time))%> ~<%time/50%> ~<%(Math.cos(time))%> 0 0 0 0 2 force
        $execute as @e[tag=bat_particles] at @s run particle $(text) ^ ^ ^ 0 0 0 0 1 force
        }
        }
        function perlin_worms {
            execute as @e[tag=book_particles,scores={Timer=0..10}] at @s run summon bat ~ ~ ~ {Silent:1b,Invisible:1b,Invulnerable:1b,Tags:["bat_particles"]}
            execute as @e[tag=book_particles,scores={Timer=130..}] at @s run tp @e[tag=bat_particles,limit=5,sort=nearest] ~ ~100 ~
            execute as @e[tag=book_particles,scores={Timer=130..}] at @s run kill @e[tag=bat_particles,limit=5,sort=nearest]
        }
        clock time 1t {
        effect give @e[tag=bat_particles] minecraft:invisibility 2 0 true
        function main_magic:book_create_animation/particles/perlin_worms
        function main_magic:book_create_animation/particles/fx with storage magic:particles
        scoreboard players add @e[tag=book_particles] Timer 1
        }
    }
}

#This makes it initiate the crafting when the correct blocks are in place and item is present
#Basically just makes a crafting table function for magic books
dir initiate_crafts {
    dir check_block{
        function main {
            $execute as @e[type=item,nbt={Item:{id:"minecraft:book",count:1}}] at @s if block ~ ~-1 ~ crafting_table if block ~-1 ~-1 ~2 #minecraft:candles[lit=false] if block ~1 ~-1 ~2 #minecraft:candles[lit=false] if block ~2 ~-1 ~1 #minecraft:candles[lit=false] if block ~2 ~-1 ~-1 #minecraft:candles[lit=false] if block ~-1 ~-1 ~-2 #minecraft:candles[lit=false] if block ~1 ~-1 ~-2 #minecraft:candles[lit=false] if block ~-2 ~-1 ~-1 #minecraft:candles[lit=false] if block ~-2 ~-1 ~1 #minecraft:candles[lit=false] if block ~-1 ~-1 ~1 $(text) if block ~ ~-1 ~1 $(text) if block ~1 ~-1 ~1 $(text) if block ~-1 ~-1 ~ $(text) if block ~1 ~-1 ~ $(text) if block ~-1 ~-1 ~-1 $(text) if block ~ ~-1 ~-1 $(text) if block ~1 ~-1 ~-1 $(text) run function main_magic:initiate_crafts/check_block/success
        }
        function success {
                summon marker ~ ~ ~ {Tags:["Starting_Animation_CBlock"]}
                summon minecraft:lightning_bolt ~ ~ ~
                setblock ~ ~-1 ~ minecraft:oak_slab[type=double,waterlogged=true]
        }
        function animate {
            execute as @e[tag=Starting_Animation_CBlock,scores={Timer=0..100}] at @s run particle minecraft:portal ~ ~ ~ 0 0 0 1 3 normal
            execute as @e[tag=Starting_Animation_CBlock,scores={Timer=0..1}] at @s run weather rain 1d 
            execute as @e[tag=Starting_Animation_CBlock,scores={Timer=100..101}] at @s run playsound minecraft:block.candle.extinguish block @a ~ ~ ~ 20 1 0
            execute as @e[tag=Starting_Animation_CBlock,scores={Timer=100..101}] at @s run fill ~-2 ~-2 ~-2 ~2 ~2 ~2 candle[lit=true] replace minecraft:candle
            
        }
        function destroyed{
            execute as @e[tag=Starting_Animation_CBlock] at @s unless block ~ ~-1 ~ minecraft:oak_slab[type=double,waterlogged=true] run setblock ~ ~-1 ~ minecraft:air
            execute as @e[tag=Starting_Animation_CBlock] at @s unless block ~ ~-1 ~ minecraft:oak_slab[type=double,waterlogged=true] run particle minecraft:witch ~ ~1 ~ 0.5 0.5 0.5 0.1 50 normal
            execute as @e[tag=Starting_Animation_CBlock] at @s unless block ~ ~-1 ~ minecraft:oak_slab[type=double,waterlogged=true] run playsound minecraft:entity.ender_eye.death block @a ~ ~ ~ 20 1 0
            execute as @e[tag=Starting_Animation_CBlock] at @s unless block ~ ~-1 ~ minecraft:oak_slab[type=double,waterlogged=true] run fill ~-2 ~-2 ~-2 ~2 ~2 ~2 candle[lit=false] replace minecraft:candle
            execute as @e[tag=Starting_Animation_CBlock] at @s unless block ~ ~-1 ~ minecraft:oak_slab[type=double,waterlogged=true] run kill @s
        }
        clock time 35t {
            function main_magic:initiate_crafts/check_block/main with storage magic:carpet
        }
        clock animation 1t {
            execute as @e[tag=Starting_Animation_CBlock] at @s unless entity @s[scores={Timer=400..}] run scoreboard players add @s Timer 1
            
            function main_magic:initiate_crafts/check_block/animate
            function main_magic:initiate_crafts/check_block/destroyed

        }
    }
}
#This creates extra requirements for crafting magic items, such as jumping
dir crafting_extra {
    function other_requirements {
        execute as @a at @s unless entity @n[type=marker,tag=Starting_Animation_CBlock,distance=..5] run scoreboard players set @s Jump 0
    }
    clock time 1t {
        function main_magic:crafting_extra/other_requirements
    }
}

dir player_data {
    function mana_on_start {
        scoreboard players set @a Mana 100
        tag @s add Player_Magic_Start
    }
    function mana_regen {
        scoreboard players add @a[scores={Mana=..99}] Mana 1
    }
    clock time 20t {
        function main_magic:player_data/mana_regen
    }
    function tick_mana {
        tick {
            execute as @a at @s unless entity @s[tag=Player_Magic_Start] run function main_magic:player_data/mana_on_start
            execute as @a if items entity @s weapon.* *[minecraft:custom_data~{mana:1b}] run title @s actionbar ["",{"text":"-<","color":"dark_purple"},{"text":"Mana ","color":"light_purple"},{"color":"light_purple","score":{"objective":"Mana","name":"@s"}},{"text":">-","color":"dark_purple"}]
        }
    }
}
